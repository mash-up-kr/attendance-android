plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def naver_client_id = properties.getProperty('naver.client.id')

android {
    lintOptions {
        disable "JvmStaticProvidesInObjectDetector", "FieldSiteTargetOnQualifierAnnotation",
                "ModuleCompanionObjects", "ModuleCompanionObjectsNotInModuleParent"
    }
    signingConfigs {
        release {
            storeFile file('mashupKey')
            storePassword 'mashup1234!'
            keyPassword 'mashup1234!'
            keyAlias 'key'
        }
    }
    compileSdk compileVersion

    defaultConfig {
        applicationId "com.mashup"
        minSdkVersion minVersion
        targetSdkVersion targetVersion
        versionCode 28
        versionName "1.5.0"

        testInstrumentationRunner "com.mashup.core.testing.MashUpTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "API_KEY", properties["api.key"]
    }

    sourceSets {
        release {
            java.srcDirs = ['src/main/java', 'src/release/java']
        }

        debug {
            java.srcDirs = ['src/main/java', 'src/debug/java']
        }
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = ["appName":"@string/app_name_debug", "naver_client_id": naver_client_id]
            minifyEnabled false
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            manifestPlaceholders = ["appName":"@string/app_name", "naver_client_id": naver_client_id]
            minifyEnabled true
            shrinkResources true
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi",
                "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        ]
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeCompiler
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(":core:ui")
    implementation project(":core:model")
    implementation project(":core:testing")
    implementation project(":core:common")
    implementation project(":core:network")
    implementation project(":core:datastore")
    implementation project(":core:firebase")
    implementation project(":core:data")
    implementation project(":feature:setting")
    implementation project(":feature:danggn")
    implementation project(":feature:myPage")

    // ml Kit
    implementation "com.google.mlkit:barcode-scanning:$barcodeSacnnerVersion"

    // camera
    implementation "androidx.camera:camera-camera2:$cameraVersion"
    implementation "androidx.camera:camera-lifecycle:$cameraVersion"
    implementation "androidx.camera:camera-view:$cameraVersion"

    // hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpInterceptorVerison"

    // image
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:$dataSourceVersion"

    // serialization
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    // compose webview
    implementation "com.google.accompanist:accompanist-webview:$composeWebView"
    implementation "androidx.compose.ui:ui-viewbinding:$composeUiViewBinding"
    implementation "androidx.constraintlayout:constraintlayout-compose:$constraintlayoutCompose"


    // lifecycle viewmodel compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"

    // coroutine test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutineVersion"

    // flipper
    implementation "com.facebook.flipper:flipper:$flipperVersion"
    implementation "com.facebook.soloader:soloader:$soLoaderVersion"
    implementation "com.facebook.flipper:flipper-network-plugin:$flipperVersion"

    // Naver Map
    implementation "io.github.fornewid:naver-map-compose:1.4.0"
}